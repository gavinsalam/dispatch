C ROUTINE TO TEST WHETHER A REAL*8 NUMBER IS NOT-A-NUMBER
C TESTS WORD ORDER FROM FLOATING POINT REPRESENTATION OF 1D0.

C WORKS ON X86 UNDER MS-FORTRAN
C WORKS ON SUN (THWGS).

C THIS TRIES TO BE IEEE.
C JOHN COLLINS.  19 DEC. 1996.  MAKE TEST WORK FOR NEGATIVE NAN'S.
C JOHN COLLINS.  12 DEC. 1996.

C REAL*8 IN HEX:
C  INF = 7FF0 0000 0000 0000
C -INF = FFF0 0000 0000 0000
C  NAN = 7FFX XXXX XXXX XXXX, WITH NOT ALL X'S ZERO
C -NAN = FFFX XXXX XXXX XXXX, WITH NOT ALL X'S ZERO

C REAL*4IN HEX:
C  INF = 7F80 0000
C -INF = FF80 0000
C  NAN = 7F8X XXXX, WITH NOT ALL X'S ZERO
C -NAN = FF8X XXXX, WITH NOT ALL X'S ZERO

      INTEGER FUNCTION INTNAN8(D)
C  DETERMINE WHETHER D IS NOT-A-NUMBER.
C  RETURN VALUE: 1 FOR TRUE, 0 IS FALSE.
      IMPLICIT NONE
      REAL*8 D
      LOGICAL ISNAN8
      EXTERNAL ISNAN8
      IF (ISNAN8(D)) THEN
         INTNAN8 = 1
      ELSE
         INTNAN8 = 0
      ENDIF
      END

      INTEGER FUNCTION INTNAN4(R)
C  DETERMINE WHETHER R IS NOT-A-NUMBER.
C  RETURN VALUE: 1 FOR TRUE, 0 IS FALSE.
      IMPLICIT NONE
      REAL R
      LOGICAL ISNAN4
      EXTERNAL ISNAN4
      IF (ISNAN4(R)) THEN
         INTNAN4 = 1
      ELSE
         INTNAN4 = 0
      ENDIF
      END

      LOGICAL FUNCTION ISDNAN(D)
      IMPLICIT NONE
      REAL*8 D
      LOGICAL ISNAN8
      EXTERNAL ISNAN8
C  DETERMINE WHETHER D IS NOT-A-NUMBER.
      ISDNAN = ISNAN8(D)
      END


      LOGICAL FUNCTION ISNAN8 (D)
      IMPLICIT NONE
      REAL*8 D
C  DETERMINE WHETHER D IS NOT-A-NUMBER.

C     THE FOLLOWING IN FIRST HALF OF REAL*8 INDICATES NAN
C     SIGN BIT = 0, MANTISSA = 7FF
      INTEGER*4 FLAGPOS, FLAGNEG
      PARAMETER (FLAGPOS=((7*16+15)*16+15)*16**5) !HEX 7FF00000
      PARAMETER (FLAGNEG=-16**5)                  !HEX FFF00000
CC      PARAMETER (FLAGNEG=-16**4)                  !HEX FFF00000
      INTEGER*4 ITEST(2), IONE(2)
      REAL*8 DCOPY, DONE
      EQUIVALENCE (DCOPY, ITEST), (DONE, IONE)
      INTEGER WHICH

      DONE = 1D0
      DCOPY = D
      IF (IONE(1) .EQ. 0) THEN
C        WE ARE HAVE BYTE ORDER OF X86.
         WHICH = 2
      ELSE
C        WE HAVE BYTE ORDER OF SPARC.
         WHICH = 1
      ENDIF
      ISNAN8 = (ITEST(WHICH) .GE. FLAGPOS)
     > .OR. ((ITEST(WHICH) .GE. FLAGNEG) .AND. (ITEST(WHICH) .LT. 0))
C THIS INCLUDES THE CASE OF INFINITY, WHICH IS 7FF00000 00000000
      END

C=============================================

      LOGICAL FUNCTION ISNAN4 (F)
      IMPLICIT NONE
      REAL*4 F
C  DETERMINE WHETHER F IS NOT-A-NUMBER.

C     THE FOLLOWING IN FIRST HALF OF REAL*8 INDICATES NAN
C     SIGN BIT = 0, MANTISSA = 7F8
      INTEGER*4 FLAGPOS, FLAGNEG
      PARAMETER (FLAGPOS=((7*16+15)*16+8)*16**5)  !HEX 7F800000
      PARAMETER (FLAGNEG=-8*16**4)                !HEX FF800000
      INTEGER*4 ITEST
      REAL*4 FCOPY
      EQUIVALENCE (FCOPY, ITEST)

      FCOPY = F
      ISNAN4 = (ITEST .GE. FLAGPOS)
     > .OR. ((ITEST .GE. FLAGNEG) .AND. (ITEST .LT. 0))
C THIS INCLUDES THE CASE OF INFINITY, WHICH IS 7F800000
      END
